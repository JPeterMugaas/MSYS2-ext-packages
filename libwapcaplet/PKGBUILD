# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=libwapcaplet
pkgbase=${_realname}
pkgname=("${_realname}" "${_realname}-devel")
pkgver=0.4.1
pkgrel=1
pkgdesc="String internment library with rapid string comparison (mingw-w64)"
arch=('any')
url='http://www.netsurf-browser.org/projects/libwapcaplet/'
license=('MIT')
makedepends=("cmake")
#checkdepends=("check")
options=('strip' 'staticlibs')
# no https available
source=("http://download.netsurf-browser.org/libs/releases/${_realname}-$pkgver-src.tar.gz"
        "CMakeLists.txt")
sha256sums=('c8ff6ea5b64feda806f5b79ba970442f5edb237046a044bc0f7e4d54a9a4978c'
            'SKIP')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  cp ../CMakeLists.txt .
#  apply_patch_with_msg 0001-A-really-important-fix.patch \
#    0002-A-less-important-fix.patch
}

build() {
  cd "${srcdir}"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      "${extra_config[@]}" \
      -DBUILD_{SHARED,STATIC}_LIBS=ON \
      ../${_realname}-${pkgver}

  make
  make install DESTDIR="${srcdir}/dest"
}


check() {
  cd "${srcdir}"/build-${CARCH}
  make test || true
}

package_libwapcaplet() {
  cd ${srcdir}/dest
  mkdir -p "${pkgdir}/usr/bin"
  cp -f usr/bin/*.dll "${pkgdir}/usr/bin"
  # license
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${_realname}/COPYING
}

package_libwapcaplet-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libwapcaplet=${pkgver}")
  pkgdesc="Headers and static library for libwapcaplet"

  cd ${srcdir}/dest
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include $pkgdir/usr/
  cp -rf ${srcdir}/dest/usr/lib $pkgdir/usr/
}
