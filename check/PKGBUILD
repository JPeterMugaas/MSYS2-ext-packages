# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=check
pkgbase=${_realname}
pkgname="${_realname}"
pkgver=0.12.0
pkgrel=1
pkgdesc="A unit testing framework for C"
arch=('any')
url="https://libcheck.github.io/check/"
license=("LGPL")
depends=("gcc-libs")
options=('strip' 'staticlibs')
source=(https://github.com/libcheck/check/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz
        check-0.12-msys2.patch)
sha256sums=('464201098bee00e90f5c4bdfa94a5d3ead8d641f9025b560a27755a83b824234'
            '6243e877cc29adbde4fa99c1234c69e9e473295631212a9d1040d06db45a292f')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Nbp1 -i "${srcdir}/check-0.12-msys2.patch"
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=/usr \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
